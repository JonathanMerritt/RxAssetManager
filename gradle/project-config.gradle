/*
 *     Copyright 2018 Jonathan Merritt 11R00TT00R11@GMAIL.COM
 *     
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

ext {
  dev = [
      name  : "jonathanmerritt",
      email : "11R00TT00RR11@GMAIL.COM"
  ]

  def local = new Properties()
  local.load(file("local.properties").newDataInputStream())
  dev.bintray = [
      key : local.getProperty("bintray.key")
  ]
  dev.gpg = [
      pass : local.getProperty("gpg.pass")
  ]
  dev.maven = [
      pass : local.getProperty("maven.pass")
  ]


  rep = [
      name : rootProject.name
  ]
  rep += [
      id   : "com.github.${dev.name}.${rep.name.toLowerCase()}",
      root : "$dev.name/$rep.name"
  ]
  rep += [
      url : "https://github.com/$rep.root"
  ]

  rep.vcs = [
      url : "scm:git:git://github.com/${rep.root}.git",
      dev : "scm:git:git@github.com/${rep.root}.git"
  ]

  rep.labels = [
      "android",
      "android-library",
      "asset",
      "assets",
      "assetmanager",
      "assetsmanager",
      "android-asset",
      "android-assets",
      "android-asset-manager",
      "android-assets-manager"
  ]

  rep.licenses = [
      "Apache-2.0"
  ]

  rep.sdk = [
      compile : 'android-P',
      min     : 17,
      tools   : '28.0.0 rc1'
  ]

  dep = [
      guava      : "com.google.guava:guava:23.6-android",
      rxjava2Ext : "com.github.akarnokd:rxjava2-extensions:0.17.0",
      timber     : "com.jakewharton.timber:timber:4.5.1"
  ]

  def support = [
      group   : "com.android.support",
      version : "28.0.0-alpha1"
  ]
  dep.support = [
      appcompat : "$support.group:appcompat-v7:$support.version",
      v4        : "$support.group:support-v4:$support.version",
      design    : "$support.group:design:$support.version"
  ]

  support.constraint = [
      group   : "${support.group}.constraint",
      version : "1.1.0-beta5"
  ]
  dep.support.constraint = [
      "$support.constraint.group:constraint-layout:$support.constraint.version",
      "$support.constraint.group:constraint-layout-solver:$support.constraint.version"
  ]

  support.test = "${support.group}.test"
  dep.support.test = [
      "$support.test:runner:1.0.1",
      "${support.test}.espresso:espresso-core:3.0.1"
  ]

  def rxjava2 = "io.reactivex.rxjava2"
  dep.rxjava2 = [
      "$rxjava2:rxjava:2.1.7",
      "$rxjava2:rxandroid:2.0.1"
  ]

  dep.test = [
      junit : "junit:junit:4.12"
  ]
}

subprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  beforeEvaluate {
    ext {
      rep.lib = [
          name : name
      ]
      rep.lib += [
          url  : "$rep.url/tree/master/$rep.lib.name",
          desc : "${rep.name}s' $rep.lib.name library"
      ]

      rep.version = [
          name : rep.lib.name.contains('app') ? "0.0.1" : "0.0.1"
      ]
      rep.version += [
          code : rep.version.name.replace(".", "").concat(new Date().format("MMddyy")).toBigInteger()
      ]
    }
  }
}