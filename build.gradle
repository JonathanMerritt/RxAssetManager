/*
 *     Copyright 2018 Jonathan Merritt 11R00TT00R11@GMAIL.COM
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

buildscript {
  /* DEVELOPER */
  def DEV = [:]
  DEV.name = "jonathanmerritt"
  DEV.email = "11r00tt00r11@gmail.com"

  // properties
  def p = new Properties()
  p.load(file("local.properties").newDataInputStream())

  // passwords
  def pass = [:]
  pass.bintray = p.getProperty("pass.bintray")
  pass.gpg = p.getProperty("pass.gpg")
  pass.maven = p.getProperty("pass.maven")
  DEV.pass = pass
  ext.DEV = DEV

  /* REPOSITORY */
  def REP = [:]
  REP.name = rootProject.name
  REP.id = "com.github.${DEV.name}.${REP.name.toLowerCase()}"
  REP.labels = ["android", "android-library", "asset", "assets", "assetmanager", "assetsmanager", "android-asset",
                "android-assets", "android-asset-manager", "android-assets-manager"]
  REP.licenses = ["Apache-2.0"]
  ext.REP = REP

  /* SDKS */
  def SDK = [:]
  SDK.compile = "android-P"
  SDK.target = 27
  SDK.min = 17
  SDK.tools = "28.0.0-rc2"
  ext.SDK = SDK

  /* DEPENDENCIES */
  def DEP = [:]

  // groups
  def g = [:]
  g.constraint = "androidx.constraintlayout"
  g.kotlin = "org.jetbrains.kotlin"
  g.rxjava2 = "io.reactivex.rxjava2"

  // versions
  def v = [:]
  v.androidx = "1.0.0-alpha1"
  v.appcompat = v.androidx
  v.viewpager = v.androidx
  v.coordinator = v.androidx
  v.material = v.androidx
  v.constraint = "1.1.0"
  v.kotlin = "1.2.41"

  // androidx
  def androidx = [:]
  androidx.appcompat = "androidx.appcompat:appcompat:$v.appcompat"
  androidx.viewpager = "androidx.viewpager:viewpager:$v.viewpager"
  androidx.coordinator = "androidx.coordinatorlayout:coordinatorlayout:$v.coordinator"
  androidx.material = "com.google.android.material:material:$v.material"
  androidx.constraint = ["$g.constraint:constraintlayout:$v.constraint",
                         "$g.constraint:constraintlayout-solver:$v.constraint"]
  DEP.androidx = androidx

  // jetbrains kotlin
  def kotlin = [:]
  kotlin.gradle = "$g.kotlin:kotlin-gradle-plugin:$v.kotlin"
  kotlin.stdlib = "$g.kotlin:kotlin-stdlib:$v.kotlin"
  DEP.kotlin = kotlin

  // reactivex rxjava2
  DEP.rxjava2 = ["$g.rxjava2:rxjava:2.1.7", "$g.rxjava2:rxandroid:2.0.1"]
  ext.DEP = DEP


  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:3.2.0-alpha14"
    classpath "com.github.dcendents:android-maven-gradle-plugin:2.0"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0"
    classpath DEP.kotlin.gradle
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.16"
  }
}

subprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  beforeEvaluate {
    /* LIBRARY */
    def LIB = [:]
    LIB.name = name
    LIB.desc = "${REP.name}s' $LIB.name library"
    ext.LIB = LIB

    /* VERSION */
    def VER = [:]
    VER.name = LIB.name.contains("app") ? "2.0.0" : "0.6.0"
    VER.code = VER.name.replace(".", "").concat(new Date().format("MMddyy")).toBigInteger()
    ext.VER = VER
  }
}

task clean(type : Delete) {
  delete rootProject.buildDir
}
