/*
 *     Copyright 2018 Jonathan Merritt 11R00TT00R11@GMAIL.COM
 *
 *     Licensed under the Apache License, Version 2.0 (the "License");
 *     you may not use this file except in compliance with the License.
 *     You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *     Unless required by applicable law or agreed to in writing, software
 *     distributed under the License is distributed on an "AS IS" BASIS,
 *     WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *     See the License for the specific language governing permissions and
 *     limitations under the License.
 */

buildscript {
  /*
  Developer configuration
  */
  def dev = [:]
  dev.name = "jonathanmerritt"
  dev.email = "11R00TT00RR11@GMAIL.COM"
  ext.dev = dev

  def pass = [:]
  def local = new Properties()
  local.load(file("local.properties").newDataInputStream())
  pass.bintray = local.getProperty("pass.bintray")
  pass.gpg = local.getProperty("pass.gpg")
  pass.maven = local.getProperty("pass.maven")
  ext.dev.pass = pass

  /*
  Repository configuration
  */
  def rep = [:]
  rep.name = rootProject.name
  rep.id = "com.github.${dev.name}.${rep.name.toLowerCase()}"
  rep.labels = ["android", "android-library", "asset", "assets", "assetmanager", "assetsmanager", "android-asset",
                "android-assets", "android-asset-manager", "android-assets-manager"]
  rep.licenses = ["Apache-2.0"]
  ext.rep = rep

  /*
  Sdk configuration
  */
  def sdk = [:]
  sdk.compile = 27
  sdk.min = 17
  sdk.tools = '27.0.3'
  ext.sdk = sdk

  /*
  Dependency configuration
  */
  def dep = [:]
  dep.rxjava2Ext = "com.github.akarnokd:rxjava2-extensions:0.17.0"
  dep.timber = "com.jakewharton.timber:timber:4.5.1"
  ext.dep = dep

  ext.dep.test = ["junit:junit:4.12"]

  def ver_support = "27.1.0"
  def ver_constraint = "1.1.0-beta6"
  def ver_kotlin = "1.2.31"

  def support = [:]
  support.appcompat = "com.android.support:appcompat-v7:$ver_support"
  support.v4 = "com.android.support:support-v4:$ver_support"
  support.design = "com.android.support:design:$ver_support"
  support.constraint = ["com.android.support.constraint:constraint-layout:$ver_constraint",
      "${"com.android.support"}.constraint:constraint-layout-solver:$ver_constraint"]
  support.test = ["com.android.support.test:runner:1.0.1", "com.android.support.test.espresso:espresso-core:3.0.1"]
  ext.dep.support = support

  def kotlin = [:]
  kotlin.gradle = "org.jetbrains.kotlin:kotlin-gradle-plugin:$ver_kotlin"
  kotlin.stdlib = "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$ver_kotlin"
  ext.dep.kotlin = kotlin

  ext.dep.rx2 = ["io.reactivex.rxjava2:rxjava:2.1.7", "io.reactivex.rxjava2:rxandroid:2.0.1"]


  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    // Last version to not cause Android Studio gradle editor bug.
    // Issue link https://issuetracker.google.com/issues/74489623
    //noinspection all
    classpath 'com.android.tools.build:gradle:3.2.0-alpha01'

    classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    classpath dep.kotlin.gradle
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.16"
  }
}

subprojects {
  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  beforeEvaluate {
    /*
    Library configuration
    */
    def lib = [:]
    lib.name = name
    lib.desc = "${rep.name}s' $lib.name library"
    ext.lib = lib

    /*
    Version configuration
    */
    def ver = [:]
    ver.name = lib.name.contains('app') ? "1.0.0" : "0.6.1"
    ver.code = ver.name.replace(".", "").concat(new Date().format("MMddyy")).toBigInteger()
    ext.ver = ver
  }
}

task clean(type : Delete) {
  delete rootProject.buildDir
}
